buildscript {
  repositories {
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath 'io.fabric.tools:gradle:1.+'
  }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../signing.gradle'

repositories {
  maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'jacoco'

android {
  dataBinding {
    enabled = true
  }
  packagingOptions {
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }
  compileSdkVersion 25
  buildToolsVersion "25.0.3"

  defaultConfig {
    applicationId "com.fred.inventory"
    minSdkVersion 22
    targetSdkVersion 25
    versionCode 5
    versionName "1.3.1-bloodstone"
    vectorDrawables.useSupportLibrary = true
  }
  signingConfigs {
    release {
      storeFile suppliesStoreFile
      storePassword suppliesStorePassword
      keyAlias suppliesKeyAlias
      keyPassword suppliesKeyPassword
    }
    debug {
      storeFile file('../supplies-debug.jks')
      storePassword 'supplies'
      keyAlias 'supplies'
      keyPassword 'supplies'
    }
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationVariants.all { variant ->
        variant.outputs.each { output ->
          output.outputFile = new File(output.outputFile.parent,
              output.outputFile.name.replace("app", "supplies-${versionName}"))
        }
      }
    }
    debug {
      debuggable true
      minifyEnabled false
      applicationVariants.all { variant ->
        variant.outputs.each { output ->
          output.outputFile = new File(output.outputFile.parent,
              output.outputFile.name.replace("app", "supplies-${versionName}"))
        }
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

def supportLibrariesVersion = '25.3.1'

dependencies {
  annotationProcessor 'com.squareup.dagger:dagger-compiler:1.2.2'
  annotationProcessor 'com.github.frankiesardo:icepick-processor:2.3.6'
  annotationProcessor 'com.google.auto.value:auto-value:1.3'
  annotationProcessor 'me.mattlogan.auto.value:auto-value-firebase:1.1.0'
  annotationProcessor 'me.mattlogan.auto.value:auto-value-firebase-annotation:1.1.0'
  provided 'me.mattlogan.auto.value:auto-value-firebase-annotation:1.1.0'
  provided 'com.google.auto.value:auto-value:1.3'

  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile "com.android.support:appcompat-v7:$supportLibrariesVersion"
  compile "com.android.support:cardview-v7:$supportLibrariesVersion"
  compile "com.android.support:design:$supportLibrariesVersion"
  compile 'com.squareup.retrofit:retrofit:1.9.0'
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
  compile 'com.squareup.okhttp:okhttp:2.0.0'
  compile 'com.squareup.dagger:dagger:1.2.2'
  compile 'com.jakewharton.timber:timber:4.1.1'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  compile 'com.github.frankiesardo:icepick:2.3.6'
  compile 'com.github.clans:fab:1.6.4'
  compile 'com.journeyapps:zxing-android-embedded:3.4.0'
  compile 'com.google.firebase:firebase-auth:10.2.0'
  compile 'com.google.firebase:firebase-database:10.2.0'
  compile 'com.google.android.gms:play-services-auth:10.2.0'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'me.mattlogan.auto.value:auto-value-firebase-adapter:1.1.0'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.0.39-beta'
  testCompile('com.squareup.assertj:assertj-android:1.0.0') {
    exclude module: 'support-annotations'
  }
  compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
    transitive = true;
  }
}

task jacocoTestReport(type: JacocoReport) {
  group = "Reporting"
  description = "Generate Jacoco coverage reports"

  classDirectories =
      fileTree(dir: "${project.rootDir}/${project.name}/build/intermediates/classes/debug",
          excludes: project.jacocoExcludes)

  additionalSourceDirs = files(project.coverageSourceDirs)
  sourceDirectories = files(project.coverageSourceDirs)
  executionData = fileTree(dir: 'build/jacoco', include: '**/*.exec')

  reports {
    xml.enabled = true
    html.enabled = true
  }
}

apply plugin: 'com.google.gms.google-services'

